# Contributor: WAntilles <wantilles@adslgr.com>

pkgname=compiz-decorator-gtk
pkgver=0.8.4
pkgrel=1
pkgdesc="Decorator of Compiz for GNOME"
url="http://www.go-compiz.org/"
license=('GPL2' 'LGPL2' 'MIT')
arch=('i686' 'x86_64')
depends=('gnome-control-center>=2.28.0' "compiz-core>=${pkgver}")
makedepends=('metacity>=2.28.0' 'intltool>=0.23')
groups=('compiz-fusion-new' 'compiz-fusion-gtk-new')
options=('!libtool' '!emptydirs')
conflicts=('compiz-git' 'compiz-decorator-gtk-git')
source=(http://releases.compiz.org/core/compiz-${pkgver}.tar.bz2)
install=compiz-decorator-gtk.install
md5sums=('03fa78ce0c464f3a413b2a3b74f09559')

build()
{
  cd ${srcdir}/compiz-${pkgver}
  
  ./configure --prefix=/usr --sysconfdir=/etc \
	    --enable-gnome \
	    --disable-librsvg \
	    --enable-metacity \
	    --enable-gtk \
	    --enable-gconf \
	    --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --disable-dbus \
	    --disable-annotate \
	    --disable-place \
	    --disable-kde

  make

  pushd gtk
  make DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  install -m644 window-decorator/gwd.schemas \
                ${pkgdir}/usr/share/gconf/schemas/gwd.schemas
  popd

  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm ${srcdir}/compiz-${pkgver}/metadata/compiz-$i.schemas
  done

  gconf-merge-schema \
    ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
      	${srcdir}/compiz-${pkgver}/metadata/*.schemas

  rm -rf ${pkgdir}/etc
}

