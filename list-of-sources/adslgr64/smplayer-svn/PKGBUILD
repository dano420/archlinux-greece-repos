# Contributor: WAntilles <wantilles@adslgr.com>

pkgname=smplayer-svn
realname=smplayer
pkgver=3591
pkgrel=1
lateststablever=0.6.9
pkgdesc="A complete front-end for MPlayer" 
arch=('i686' 'x86_64')
sourcetarballdir=http://archlinuxgr.tiven.org/sources
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt>=4.7.1' 'mplayer>=32492')
conflicts=('smplayer')
provides=("smplayer=${lateststablever}")
groups=('video-playback-svn')
source=(${sourcetarballdir}/${pkgname}-${pkgver}.tar.xz
	optionally-enable-vdpau-divx-decoder.patch)

build() { 
  cd ${srcdir}/${realname}

  # remove broken translation
  sed -i "s|translations/smplayer_gl.ts|#translations/smplayer_gl.ts|" src/smplayer.pro

  # give the option to enable vdpau divx decoding through smplayer.ini
  patch -Np2 -i ${srcdir}/optionally-enable-vdpau-divx-decoder.patch

  make PREFIX=/usr
}

package() { 
  cd ${srcdir}/${realname}
  make PREFIX=${pkgdir}/usr install

  install -dm755 ${pkgdir}/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/64x64/apps/smplayer.png \
        ${pkgdir}/usr/share/pixmaps/

  sed -i 's|Exec=smplayer %U|Exec=smplayer %F|' ${pkgdir}/usr/share/applications/smplayer.desktop
  sed -i 's|Exec=smplayer -add-to-playlist %U|Exec=smplayer -add-to-playlist %F|' \
  	${pkgdir}/usr/share/applications/smplayer_enqueue.desktop
}

md5sums=('223595223bc41516aa36a1b62124a2c6'
         'a566d474a3605b4bf122c957d95a778f')
