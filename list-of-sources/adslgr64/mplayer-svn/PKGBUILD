# Contributor: WAntilles <wantilles@adslgr.com>

pkgname=mplayer-svn
pkgver=32636
pkgrel=1
pkgdesc="A movie player for linux"
arch=('i686' 'x86_64')
sourcetarballdir=http://archlinuxgr.tiven.org/sources
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl'
	 'lame>=3.98.3' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient'
	 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'libxvmc' 'libass>=0.9.9'
	 'libvdpau>=0.4' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx'
	 'libvorbis>=1.3.1' 'libbs2b>=3.1.0' 'ladspa>=1.13' 'mpg123>=1.12.1'
	 'faad2>=2.7' 'libdvdcss>=1.2.10' 'libbluray>=0.1')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
replaces=('mplayer-nogui-dvdnav-amr-vdpau-svn' 'mencoder-svn')
conflicts=('mplayer' 'mencoder-svn')
provides=("mplayer=${pkgver}" "mencoder=${pkgver}")
groups=('video-playback-svn' 'video-encoding-svn')
source=(${sourcetarballdir}/${pkgname}-${pkgver}.tar.xz)

build() {
  cd ${srcdir}/mplayer

  # Custom CFLAGS and LDFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  ./configure --prefix=/usr \
	--enable-runtime-cpudetection \
	--disable-gui \
	--disable-arts \
	--disable-liblzo \
	--disable-speex \
	--disable-openal \
	--disable-fribidi \
	--disable-libdv \
	--disable-musepack \
	--disable-esd \
	--disable-mga \
	--enable-xvmc \
	--language=all \
      	--confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
}

package() {
  cd ${srcdir}/mplayer
  make -j1 DESTDIR=${pkgdir} install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  install -dm755 ${pkgdir}/usr/share/mplayer/
  rm -rf ${pkgdir}/usr/share/mplayer/font

  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/share/mplayer
}

md5sums=('340d456c9bea16c668f0217fc50c2072')
