# Contributor: WAntilles <wantilles@adslgr.com>

pkgname=nvidia-utils-beta
pkgver=195.30
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86 	 
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('xorg-server')
optdepends=('gtk2: nvidia-settings' 'pkg-config: nvidia-xconfig')
conflicts=('libgl' 'nvidia-utils')
provides=('libgl' "nvidia-utils=${pkgver}-${pkgrel}")
license=('custom')
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run")
md5sums=('a95834e46435920472c53b53509cf331')
[ "$CARCH" = "x86_64" ] && md5sums=('30d27016b42b943bf8aaaf5a1e18b050')

build() {
	cd $srcdir
	sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr/

	mkdir -p $pkgdir/usr/{lib,bin,share/applications,share/pixmaps,share/man/man1}
	mkdir -p $pkgdir/usr/lib/xorg/modules/{extensions,drivers}
	mkdir -p $pkgdir/usr/lib/vdpau
	mkdir -p $pkgdir/usr/share/licenses/nvidia
	mkdir -p $pkgdir/usr/include/cuda

	install -m644 include/cuda/cuda*.h $pkgdir/usr/include/cuda/

	install lib/{libGLcore,libGL,libnvidia-cfg,libcuda,tls/libnvidia-tls}.so.${pkgver} $pkgdir/usr/lib/
	install -m755 lib/vdpau/libvdpau_nvidia.so.${pkgver} "${pkgdir}/usr/lib/vdpau/"
	ln -sf libvdpau_nvidia.so.${pkgver} "${pkgdir}/usr/lib/vdpau/libvdpau_nvidia.so.1"

	install -m644 share/man/man1/* $pkgdir/usr/share/man/man1/
	rm $pkgdir/usr/share/man/man1/nvidia-installer.1.gz
	install X11R6/lib/{libXvMCNVIDIA.a,libXvMCNVIDIA.so.${pkgver}} $pkgdir/usr/lib/
	install -m644 share/applications/nvidia-settings.desktop $pkgdir/usr/share/applications/
	# fix nvidia .desktop file
	sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i $pkgdir/usr/share/applications/nvidia-settings.desktop
	install -m644 share/pixmaps/nvidia-settings.png $pkgdir/usr/share/pixmaps/
	install X11R6/lib/modules/drivers/nvidia_drv.so $pkgdir/usr/lib/xorg/modules/drivers
	install X11R6/lib/modules/extensions/libglx.so.$pkgver $pkgdir/usr/lib/xorg/modules/extensions
	install -m755 bin/nvidia-{settings,xconfig,bug-report.sh} $pkgdir/usr/bin/
	cd $pkgdir/usr/lib/
	ln -s libGL.so.$pkgver libGL.so
	ln -s libGL.so.$pkgver libGL.so.1
	ln -s libGLcore.so.$pkgver libGLcore.so.1
	ln -s libnvidia-cfg.so.$pkgver libnvidia-cfg.so.1
	ln -s libnvidia-tls.so.$pkgver libnvidia-tls.so.1
	ln -s libcuda.so.$pkgver libcuda.so.1
	ln -s libcuda.so.$pkgver libcuda.so
	ln -s libXvMCNVIDIA.so.$pkgver libXvMCNVIDIA_dynamic.so.1

	cd $pkgdir/usr/lib/xorg/modules/extensions
	ln -s libglx.so.$pkgver libglx.so

	install -m644 $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/LICENSE $pkgdir/usr/share/licenses/nvidia/
	ln -s nvidia $pkgdir/usr/share/licenses/nvidia-utils

	install -D -m644 $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr/share/doc/README.txt $pkgdir/usr/share/doc/nvidia/README

	find $pkgdir/usr -type d -exec chmod 755 {} \;
	chmod 644 $pkgdir/usr/lib/libXvMCNVIDIA.a
}

