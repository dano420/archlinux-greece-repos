# Contributor: hydro <hydro@freenet.de>

pkgname=libzen
pkgver=0.4.10
pkgrel=1
pkgdesc="shared library for libmediainfo and mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('BSD')
depends=('gcc-libs')
options=('!libtool')
source=(http://downloads.sourceforge.net/zenlib/${pkgname}_${pkgver}.tar.bz2)
md5sums=('046eed44ac17faea981319bc3a70fbb7')

build() {
  cd $srcdir/ZenLib/Project/GNU/Library
  chmod +x autogen
  ./autogen
  ./configure --prefix=/usr --enable-shared
  make clean
  make
  make DESTDIR=$pkgdir install

  install -dm 755 $pkgdir/usr/include/ZenLib
  install -m 644 $srcdir/ZenLib/Source/ZenLib/*.h $pkgdir/usr/include/ZenLib

  for i in Base64 HTTP_Client; do
    install -dm 755 $pkgdir/usr/include/ZenLib/$i
    install -m 644 $srcdir/ZenLib/Source/ZenLib/$i/*.h $pkgdir/usr/include/ZenLib/$i
  done    

  install -dm 755 $pkgdir/usr/lib/pkgconfig
  install -m 644 $srcdir/ZenLib/Project/GNU/Library/libzen.pc $pkgdir/usr/lib/pkgconfig 
  sed -i -e 's|Version: |Version: '$pkgver'|g' $pkgdir/usr/lib/pkgconfig/libzen.pc
 
  install -D -m 644 $srcdir/ZenLib/License.txt $pkgdir/usr/share/licenses/libzen/License.txt

}
