# $Id:$
# Contributor: Balwinder S Dheeman <bdheeman@gmail.com>
# Credits: alexwizard. thotypous, jdhore, xduugu, randypenguin

pkgname=chromium-browser-bin
_realname=chromium-browser
pkgver=33232
pkgrel=1
pkgdesc="The open-source project behind Google Chrome"
arch=('i686' 'x86_64')
url=http://code.google.com/chromium/
license=('custom:BSD')
depends=('alsa-lib' 'desktop-file-utils' 'ffmpeg' 'gconf' 'nss')
optdepends=('chromium-codecs-ffmpeg-nonfree-bin: for viewing html5/h.264 videos'
    'xdg-utils: for making/setting it as default browser')
conflicts=('bin32-cxchromium' "${_realname}" "${_realname}-dev" "${_realname}-inspector"
    "${_realname}-l10n" "${_realname}-latest" "${_realname}-svn" 'chromium-continuous'
    'chromium-fresh' 'chromium-snapshot' 'chromium-snapshot-64' 'chromium-snapshot-64-last'
    'chromium-snapshot-latest' 'cxchromium' 'google-chrome-dev' 'google_chrome_win32-bin' 'iron')
provides=("${_realname}")
options=('emptydirs' '!strip')
backup=('etc/${_realname}/default')
install=${_realname}.install

case "$CARCH" in
    x86_64|amd64) _bldarch='chromium-rel-linux-64';;
    i686|i386|*) _bldarch='chromium-rel-linux';;
esac

_bldname=chrome-linux.zip
_bldroot=http://build.chromium.org/buildbot/snapshots/${_bldarch}

source=('LICENSE.txt' 'chrome-wrapper.patch' "${_realname}.default" "${_realname}.desktop"
	"${_realname}.sh")

# trick re-determining the build revision and, or entertain the hidden
# makepkg option --forcever, huh
if [ -z "$FORCE_VER" ]; then
    msg "Determining latest build revision..."
    newpkgver="$(wget -qO - ${_bldroot}/LATEST)"
    if [ -n "$newpkgver" -a "$newpkgver" != "$pkgver" ]; then
	msg2 "Version found: %s" "$newpkgver"
	FORCE_VER="$newpkgver"
    else
	FORCE_VER="$pkgver"
    fi
fi
if [ "$FORCE_VER" != "$pkgver" ]; then
    rm -f "$SRCDEST/${_bldname}"
    devel_update
fi

build() {
    if [ ! -r "$srcdir/${_bldname}" ]; then
	msg "Downloading the latest '$CARCH/$pkgver/${_bldname}'..."
	wget --no-cache -c "${_bldroot}/$pkgver/${_bldname}" \
	    -O "$SRCDEST/${_bldname}" || return $?
	ln -fs "$SRCDEST/${_bldname}" "$srcdir/${_bldname}" || return $?
    fi

    msg2 "Extracting Binaries..."
    cd $srcdir
    bsdtar -xf $srcdir/${_bldname} || return $?
    mkdir -p $pkgdir/opt/${_realname}
    cp -r ./chrome-linux/* $pkgdir/opt/${_realname}

    msg2 "Patching executable 'chrome'..."
    cd $pkgdir/opt/${_realname}
    sed -i 's,libnss3.so.1d,libnss3.so\x00\x00\x00,g;
	s,libnssutil3.so.1d,libnssutil3.so\x00\x00\x00,g;
	s,libsmime3.so.1d,libsmime3.so\x00\x00\x00,g;
	s,libssl3.so.1d,libssl3.so\x00\x00\x00,g;
	s,libplds4.so.0d,libplds4.so\x00\x00\x00,g;
	s,libplc4.so.0d,libplc4.so\x00\x00\x00,g;
	s,libnspr4.so.0d,libnspr4.so\x00\x00\x00,g;' chrome || return 1

    msg2 "Patching script 'chrome-wrapper'..."
    patch -p1 < $srcdir/chrome-wrapper.patch

    msg2 "Making it nice..."
    # adjust the permissions on directories and, or files, eh
    chown -R 0:0 $pkgdir/opt/${_realname}
    find $pkgdir/opt/${_realname} -type d -exec chmod 0755 {} ';'
    find $pkgdir/opt/${_realname} -type f -exec chmod 0644 {} ';'
    chmod 755 $pkgdir/opt/${_realname}/chrome
    chmod 755 $pkgdir/opt/${_realname}/chrome[_-]*
    chmod 755 $pkgdir/opt/${_realname}/xdg-settings
    # install default, wrapper script, desktop and license files
    install -Dm644 $srcdir/${_realname}.default $pkgdir/etc/${_realname}/default
    install -Dm755 $srcdir/${_realname}.sh $pkgdir/usr/bin/${_realname}
    install -Dm644 $srcdir/${_realname}.desktop $pkgdir/usr/share/applications/${_realname}.desktop
    mkdir -p $pkgdir/usr/share/man/man1
    mv $pkgdir/opt/${_realname}/chrome.1 $pkgdir/usr/share/man/man1/${_realname}.1 || return $?
    install -Dm644 $srcdir/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
    # symlink the icon file
    mkdir -p $pkgdir/usr/share/pixmaps
    ln -s /opt/${_realname}/product_logo_48.png $pkgdir/usr/share/pixmaps/${_realname}.png
}

# vim:set ts=4 sw=4 et:
md5sums=('b689219f39e74e0c0b19f10a1db1839d'
         'fda051f46b16f9a2689f9afffe27a636'
         'b11db453bbfcc98cd6817549b0559ae9'
         'e98e00cd664784b98f3dc9a0e109f42f'
         'c7b7307da94482dbba611e63548a3b2b')
